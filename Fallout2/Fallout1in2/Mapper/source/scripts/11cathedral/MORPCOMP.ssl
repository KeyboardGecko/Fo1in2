/*

   Cathedral Tower - Morpheus' computer terminal.

*/

#include "define.h"

#define NAME                    SCRIPT_MORPCOMP

#include "command.h"

procedure start;
procedure use_p_proc;
procedure use_skill_on_p_proc;

procedure use_terminal;
procedure hack_terminal;
procedure morpheus_hostile;

variable Skill := 0;

#define LVAR_Hacked        (0)

import variable Morpheus_Ptr;

procedure start begin
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(message_str(SCRIPT_MACHIV, 100));
end

procedure use_p_proc begin
   if (Morpheus_Ptr != 0) then begin
      if obj_can_see_obj(Morpheus_Ptr, dude_obj) then begin
         call morpheus_hostile;
      end
      else begin
         call use_terminal;
      end
   end
   else
      call use_terminal;
end

procedure use_skill_on_p_proc begin
   if (Morpheus_Ptr != 0) then begin
      if obj_can_see_obj(Morpheus_Ptr, dude_obj) then begin
         call morpheus_hostile;
      end
      else begin
         call hack_terminal;
      end
   end
   else
      call hack_terminal;
end

procedure use_terminal begin
   script_overrides;
   if (local_var(LVAR_Hacked) == 0) then begin
      display_msg(mstr(105));
   end
   else begin
      display_msg(mstr(107));
   end
end

procedure hack_terminal begin
   if (action_being_used == SKILL_SCIENCE) then begin
      Skill := roll_vs_skill(dude_obj, SKILL_SCIENCE, 0);
      if (is_success(Skill) or local_var(LVAR_Hacked)) then begin
         display_msg(mstr(107));
         set_local_var(LVAR_Hacked, 1);
         set_global_var(GVAR_WORLDMAPLIST_MASTERS_LAIR, 2); // Allow BoS paladins to attack here, without having seen the Cathedral cellar
      end
      else begin
         display_msg(mstr(106));
      end
      script_overrides;
   end
end

procedure morpheus_hostile begin
   float_msg(Morpheus_Ptr, gen_mstr(172), FLOAT_MSG_RED);
   attack_setup(Morpheus_Ptr, dude_obj);
end
