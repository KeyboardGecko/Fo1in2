/*

   Master's Lair - Wounded mutant - cut content.

*/

/* Include Files */
#include "define.h"
#include "area/cathedral.h"

#define NAME                    SCRIPT_SMTWO
#define TOWN_REP_VAR            (GVAR_TOWN_REP_CATHEDRAL)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure talk_p_proc;
procedure pickup_p_proc;
procedure destroy_p_proc;

procedure zamin;
procedure combat;

#define LVAR_Visible       (4)

import variable patient;
import variable lets_go;

variable tmp_hostile;
variable Only_Once := 1;

procedure start begin
   patient := self_obj;

   if Only_Once then begin
      Only_Once := 0;
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_SUPER_MUTANT_NORMAL);
   end

   if fo1in2_cut_characters_enabled then begin
      if ((days_since_visited > 0) or (local_var(LVAR_Visible) == 1)) then begin
         set_local_var(LVAR_Visible, 1);
         set_self_visible;
      end
      else begin
         set_self_invisible;
      end
   end
   else begin
      destroy_object(self_obj);
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end

   if not(combat_is_initialized) then begin
      if (self_tile == hex_elevator_0) then begin
         move_to(self_obj, 7000, 0);
      end

      if self_can_see_dude then begin
         call zamin;
         if (dude_is_armed or not(DISGUISED)) then begin
            call combat;
         end
      end

      if (lets_go == 1) then begin
         self_walk_to_tile(hex_elevator_0);
      end
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   call zamin;
   if (dude_is_armed or not(DISGUISED)) then begin
      call combat;
   end
end

procedure pickup_p_proc begin
   if source_is_dude then begin
      tmp_hostile := 1;
   end
end

procedure destroy_p_proc begin
   inc_inv_evil_crit
end

procedure zamin begin
   coc_disguise_check;
end

procedure combat begin
   tmp_hostile := 1;
end
